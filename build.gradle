import java.util.regex.Matcher
import java.util.regex.Pattern

// --------------- Plugins ---------------

plugins {
	// https://plugins.gradle.org/plugin/org.dyvil.dyvil-gradle
	id 'org.dyvil.dyvil-gradle' version '0.6.0-alpha.2' apply false
}

// *************** Shared Configuration ***************

wrapper.setDistributionType(Wrapper.DistributionType.ALL)

// --------------- Constants ---------------

ext {
	libraryDependency = 'org.dyvil:library:0.48.0-alpha.3'
	compilerDependency = 'org.dyvil:compiler:0.48.0-alpha.3'
	gensrcDependency = 'org.dyvil:gensrc:0.12.0-alpha.3'

	versionDir = 'v0.4x'
	javaFilters = [ '**/*.java' ]
	dyvilFilters = [ '**/*.dyv', '**/*.dyh', '**/*.dyvil', '**/*.dyvilh' ]
	genSrcFilters = [ '**/*.dgt', '**/*.dgs', '**/*.dgc', '**/*.dgh' ]
	binaryFilters = [ '**/*.dyo', '**/*.class' ]
	resourceFilters = [ '**/*.properties' ]

	getTagVersion = this.&getTagVersion
}

private String getTagVersion(String module = '') {
	if (!module.isEmpty() && !module.endsWith("/")) {
		module += '/'
	}

	final String tagOutput = 'git tag --sort=-version:refname'.execute([], rootDir).text
	final Pattern pattern = Pattern.compile("^${ module }v(.*)\$", Pattern.MULTILINE)
	final Matcher matcher = pattern.matcher(tagOutput)
	if (!matcher.find()) {
		throw new IllegalStateException("failed to find /$pattern/ in output:\n$tagOutput")
	}

	final String newestTag = matcher.group(0)
	final String newestVersion = matcher.group(1)
	if (System.getenv("DYVIL_RELEASE")) {
		return newestVersion
	}

	final String commitCount = "git rev-list $newestTag.. --count".execute().text.trim()
	if (commitCount == "0") {
		return newestVersion
	}
	final String commitID = 'git rev-parse --short HEAD'.execute().text.trim()
	return newestVersion + '-' + commitCount + '-' + commitID
}

allprojects {
	apply from: rootProject.file('shared.build.gradle')
}

// *************** Configuration for Root Project ***************

// --------------- Info ---------------

version = getTagVersion()
description = 'The Dyvil programming language.'
// TODO publishInfo.labels = [ 'dyvil', 'programming-language', 'jvm' ]

// --------------- Jar ---------------

jar.doFirst {
	manifest.attributes("Main-Class": project(':repl').mainClassName)
}
