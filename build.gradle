import java.util.regex.Matcher

// --------------- Plugins ---------------

plugins {
	// https://plugins.gradle.org/plugin/org.dyvil.dyvil-gradle
	id 'org.dyvil.dyvil-gradle' version '0.4.0' apply false
	// https://plugins.gradle.org/plugin/com.jfrog.bintray
	id 'com.jfrog.bintray' version '1.8.5' apply false
	// https://plugins.gradle.org/plugin/de.clashsoft.simple-publish
	id 'de.clashsoft.simple-publish' version '0.6.0' apply false
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'
	apply plugin: 'de.clashsoft.simple-publish'
}

// --------------- Info ---------------

version = getTagVersion()
description = 'The Dyvil programming language.'
publishInfo.labels = [ 'dyvil', 'programming-language', 'jvm' ]

allprojects {
	group = 'org.dyvil'

	publishInfo {
		organization = 'dyvil'
		githubRepo = 'Dyvil/Dyvil'

		license {
			name = 'BSD 3-Clause'
			url = 'https://opensource.org/licenses/BSD-3-Clause'
		}

		developer {
			id = 'clashsoft'
			name = 'Adrian Kunz'
			email = 'clashsoft@hotmail.com'
		}
	}
}

// *************** Shared Configuration ***************

wrapper.setDistributionType(Wrapper.DistributionType.ALL)

// --------------- Constants ---------------

ext {
	libraryDependency = 'org.dyvil:library:0.47.0'
	compilerDependency = 'org.dyvil:compiler:0.47.1'
	gensrcDependency = 'org.dyvil:gensrc:0.11.1'

	versionDir = 'v0.4x'
	javaFilters = [ '**/*.java' ]
	dyvilFilters = [ '**/*.dyv', '**/*.dyh', '**/*.dyvil', '**/*.dyvilh' ]
	genSrcFilters = [ '**/*.dgt', '**/*.dgs', '**/*.dgc', '**/*.dgh' ]
	binaryFilters = [ '**/*.dyo', '**/*.class' ]
	resourceFilters = [ '**/*.properties' ]

	getTagVersion = this.&getTagVersion
}

private static String getTagVersion(String module = '') {
	if (!module.isEmpty() && !module.endsWith("/")) {
		module += '/'
	}

	final Matcher matcher = 'git tag --sort=-version:refname'.execute().text =~ module + 'v(.*)'
	matcher.find()

	final String newestTag = matcher.group(0)
	final String newestVersion = matcher.group(1)
	if (System.getenv("DYVIL_RELEASE")) {
		return newestVersion
	}

	final String commitCount = "git rev-list $newestTag.. --count".execute().text.trim()
	if (commitCount == "0") {
		return newestVersion
	}
	final String commitID = 'git rev-parse --short HEAD'.execute().text.trim()
	return newestVersion + '-' + commitCount + '-' + commitID
}

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/javaCompile.gradle'
apply from: 'gradle/test.gradle'

jar.doFirst {
	manifest.attributes("Main-Class": project(':repl').mainClassName)
}

allprojects { Project project ->
	tasks.register('version') {
		doLast {
			println project.version
		}
	}

	tasks.named('bintrayUpload') {
		it.doFirst {
			if (!System.getenv('DYVIL_RELEASE')) {
				throw new IllegalStateException('DYVIL_RELEASE environment variable not specified, ' +
						'refusing to publish potential snapshots')
			}
		}
	}
}
