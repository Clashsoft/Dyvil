package dyvil.test.lang

using dyvil.test.JUnit

import java.util.Iterator
import java.util.{ List, ArrayList }

class ForTest {
	@Test func array() -> void {
		let array = [ 1, 2, 3 ]
		let list = ArrayList<int>()

		for i <- array {
			list.add(i)
		}

		list =?= List(1, 2, 3)
	}

	@Test func iterable() -> void {
		let iterable: Iterable<int> = List(1, 2, 3)
		let list = ArrayList<int>()

		for i <- iterable {
			list.add(i)
		}

		list =?= iterable
	}

	@Test func iterator() -> void {
		let iterable = List(1, 2, 3)
		let iterator = iterable.iterator
		let list = ArrayList<int>()

		for i <- iterator {
			list.add(i)
		}

		list =?= iterable
	}

	@Test func string() -> void {
		let testString = "abc"
		var string = ""

		for c <- testString {
			string += c
		}

		string =?= testString
	}

	@Test func nested() -> void {
		let results = ArrayList<(int, int)>()

		for i <- 0 .. 1 {
			for j <- 0 .. 1 {
				results.add((i, j))
			}
		}

		results =?= List((0, 0), (0, 1), (1, 0), (1, 1))
	}
}
