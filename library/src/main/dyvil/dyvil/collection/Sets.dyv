package dyvil.collection

import java.util.{ Set, SortedSet, NavigableSet }
import java.util.{ HashSet, LinkedHashSet, TreeSet, EnumSet }
import java.util.Map
import java.util.Collection
import java.util.{ Arrays, Collections }
import java.util.Comparator

// *************** Set ***************

@JavaName("Sets")
extension<type E> Set<E> {
	// =============== Operators ===============

	@Mutating
	infix func |= <type E>(lhs: Set<-E>, rhs: Set<+E>) -> void = lhs.addAll(rhs)

	@Mutating
	infix func &=         (lhs: Set<_>, rhs: Set<_>) -> void = lhs.retainAll(rhs)

	@Mutating
	infix func ^= <type E>(lhs: Set<-E>, rhs: Set<+E>) -> void {
		for e <- rhs {
			if lhs.contains(e) {
				lhs.remove(e)
			}
			else {
				lhs.add(e)
			}
		}
	}

	// =============== Static Methods ===============

	// Set<Int>()
	@JavaName("empty")
	static func apply<type E>() -> @Immutable Set<E> = Collections.emptySet<E>()

	// Set(1)
	@JavaName("of")
	static func apply<type E>(element: E) -> @Immutable Set<E> = Collections.singleton(element)

	// Set(1, 2, 3)
	@JavaName("of")
	static func apply<type E>(elements: E...) -> @Immutable Set<E>
		= new LinkedHashSet(Arrays.asList(elements...)).unmodifiableView

	// Set(withContentsOf: someCollection)
	@JavaName("copyOf")
	static func apply<type E>(explicit withContentsOf collection: Collection<+E>) -> @Immutable Set<E>
		= new LinkedHashSet(collection).unmodifiableView

	// Set<String>() { add("a"); add("b") }
	@JavaName("build")
	static func apply<type E>(builtBy configure: Set<E>.() -> void) -> @Immutable Set<E> {
		let set = new LinkedHashSet<E>()
		configure(set)
		return set.unmodifiableView
	}

	// static func apply<type E>(explicit fromMap map: Map<E, Boolean>) -> Set<E> = Collections.newSetFromMap<E>(map)

	// =============== Methods ===============

	// --------------- Views ---------------

	final func synchronizedView() -> Set<E> = Collections.synchronizedSet(this)

	final func unmodifiableView() -> Set<E> = Collections.unmodifiableSet(this)
}

// *************** SortedSet ***************

@JavaName("SortedSets")
extension<type E> SortedSet<E> {
	// =============== Static Methods ===============

	// SortedSet<Int>()
	@JavaName("empty")
	static func apply<type E>() -> @Immutable SortedSet<E> = Collections.emptySortedSet<E>()

	// =============== Methods ===============

	// --------------- Views ---------------

	final func synchronizedView() -> SortedSet<E> = Collections.synchronizedSortedSet(this)

	final func unmodifiableView() -> SortedSet<E> = Collections.unmodifiableSortedSet(this)
}

// *************** NavigableSet ***************

@JavaName("NavigableSets")
extension<type E> NavigableSet<E> {
	// =============== Static Methods ===============

	// NavigableSet<Int>()
	@JavaName("empty")
	static func apply<type E>() -> @Immutable NavigableSet<E> = Collections.emptyNavigableSet<E>()

	// =============== Methods ===============

	// --------------- Views ---------------

	final func synchronizedView() -> NavigableSet<E> = Collections.synchronizedNavigableSet(this)

	final func unmodifiableView() -> NavigableSet<E> = Collections.unmodifiableNavigableSet(this)
}

// *************** HashSet ***************

@JavaName("HashSets")
extension HashSet {
	// =============== Static Methods ===============

	// HashSet<Int>()
	@JavaName("empty")
	static func apply<type E>() -> HashSet<E> = new HashSet<E>()

	// HashSet(1)
	@JavaName("of")
	static func apply<type E>(element: E) -> HashSet<E> = new HashSet<E>().{ $0.add(element); $0 }

	// HashSet(1, 2, 3)
	@JavaName("of")
	static func apply<type E>(elements: E...) -> HashSet<E> = new HashSet<E>(Arrays.asList(elements...))

	// HashSet(withContentsOf: someCollection)
	@JavaName("copyOf")
	static func apply<type E>(explicit withContentsOf collection: Collection<+E>) -> HashSet<E>
		= new HashSet<E>(collection)

	// HashSet<Int>(capacity: 5)
	@JavaName("withCapacity")
	static func apply<type E>(explicit capacity: int) -> HashSet<E>
		= new HashSet<E>(capacity)

	// HashSet<Int>(capacity: 5, loadFactor: 0.75)
	@JavaName("withCapacity")
	static func apply<type E>(explicit capacity: int, explicit loadFactor: float) -> HashSet<E>
		= new HashSet<E>(capacity, loadFactor)
}

// *************** LinkedHashSet ***************

@JavaName("LinkedHashSets")
extension LinkedHashSet {
	// =============== Static Methods ===============

	// LinkedHashSet<Int>()
	@JavaName("empty")
	static func apply<type E>() -> LinkedHashSet<E> = new LinkedHashSet<E>()

	// LinkedHashSet(1)
	@JavaName("of")
	static func apply<type E>(element: E) -> LinkedHashSet<E> = new LinkedHashSet<E>().{ $0.add(element); $0 }

	// LinkedHashSet(1, 2, 3)
	@JavaName("of")
	static func apply<type E>(elements: E...) -> LinkedHashSet<E> = new LinkedHashSet<E>(Arrays.asList(elements...))

	// LinkedHashSet(withContentsOf: someCollection)
	@JavaName("copyOf")
	static func apply<type E>(explicit withContentsOf collection: Collection<+E>) -> LinkedHashSet<E>
		= new LinkedHashSet<E>(collection)

	// LinkedHashSet<Int>(capacity: 5)
	@JavaName("withCapacity")
	static func apply<type E>(explicit capacity: int) -> LinkedHashSet<E>
		= new LinkedHashSet<E>(capacity)

	// LinkedHashSet<Int>(capacity: 5, loadFactor: 0.75)
	@JavaName("withCapacity")
	static func apply<type E>(explicit capacity: int, explicit loadFactor: float) -> LinkedHashSet<E>
		= new LinkedHashSet<E>(capacity, loadFactor)
}

// *************** TreeSet ***************

@JavaName("TreeSets")
extension TreeSet {
	// =============== Static Methods ===============

	// --------------- Comparable ---------------

	// TreeSet<Int>()
	@JavaName("empty")
	static func apply<type E: Comparable<E>>() -> TreeSet<E> = new TreeSet<E>()

	// TreeSet(1)
	@JavaName("of")
	static func apply<type E: Comparable<E>>(element: E) -> TreeSet<E> = new TreeSet<E>().{ $0.add(element); $0 }

	// TreeSet(1, 2, 3)
	@JavaName("of")
	static func apply<type E: Comparable<E>>(elements: E...) -> TreeSet<E> = new TreeSet<E>(Arrays.asList(elements...))

	// TreeSet(withContentsOf: someCollection)
	@JavaName("copyOf")
	static func apply<type E: Comparable<E>>(explicit withContentsOf collection: Collection<+E>) -> TreeSet<E>
		= new TreeSet<E>(collection)

	// TreeSet(withContentsOf: someSortedSet)
	@JavaName("copyOf")
	static func apply<type E: Comparable<E>>(explicit withContentsOf sortedSet: SortedSet<+E>) -> TreeSet<E>
		= new TreeSet<E>(sortedSet)

	// --------------- Comparator ---------------

	// TreeSet<Int>(comparingWith: someComparator)
	@JavaName("empty")
	static func apply<type E>(explicit comparingWith c: Comparator<-E>) -> TreeSet<E> = new TreeSet<E>(c)

	// TreeSet(1, comparingWith: someComparator)
	@JavaName("of")
	static func apply<type E>(element: E, explicit comparingWith c: Comparator<-E>) -> TreeSet<E>
		= new TreeSet<E>(c).{ $0.add(element); $0 }

	// TreeSet(1, 2, 3, comparingWith: someComparator)
	@JavaName("of")
	static func apply<type E>(elements: E..., explicit comparingWith c: Comparator<-E>) -> TreeSet<E>
		= new TreeSet<E>(c).{ Collections.addAll($0, elements...); $0 }

	// TreeSet(withContentsOf: someCollection, comparingWith: someComparator)
	@JavaName("copyOf")
	static func apply<type E>(explicit withContentsOf col: Collection<+E>,
	                          explicit comparingWith comp: Comparator<-E>) -> TreeSet<E>
		= new TreeSet<E>(comp).{ $0.addAll(col); $0 }
}

// *************** EnumSet ***************

@JavaName("EnumSets")
extension EnumSet {
	// =============== Static Methods ===============

	// --------------- Typed ---------------

	// static func apply<@Reified(.OBJECT_CLASS) type E: Enum<E>>() -> EnumSet<E> = EnumSet.noneOf(class E)

	// EnumSet(of: class SomeEnum)
	@JavaName("noneOf")
	static func apply<type E: Enum<E>>(of enumClass: Class<+E>) -> EnumSet<E> = EnumSet.noneOf(enumClass)

	// EnumSet(withAllOf: class SomeEnum)
	@JavaName("allOf")
	static func apply<type E: Enum<E>>(explicit withAllOf enumClass: Class<+E>) -> EnumSet<E> = EnumSet.allOf(enumClass)

	// --------------- Default ---------------

	// EnumSet(SomeEnum.C1)
	@JavaName("of")
	static func apply<type E: Enum<E>>(element: E) -> EnumSet<E> = EnumSet.of(element)

	// EnumSet(SomeEnum.C1, SomeEnum.C2, SomeEnum.C3)
	@JavaName("of")
	static func apply<type E: Enum<E>>(elements first: E, _ rest: E...) -> EnumSet<E> = EnumSet.of(first, rest...)

	// EnumSet(withContentsOf: someCollection)
	@JavaName("copyOf")
	static func apply<type E: Enum<E>>(withContentsOf collection: Collection<+E>) -> EnumSet<E>
		= EnumSet.copyOf(collection)

	// EnumSet(withContentsOf: someEnumSet)
	@JavaName("copyOf")
	static func apply<type E: Enum<E>>(withContentsOf enumSet: EnumSet<+E>) -> EnumSet<E>
		= EnumSet.copyOf(enumSet)

	// --------------- Special ---------------

	// EnumSet(complementing: someEnumSet)
	@JavaName("complementOf")
	static func apply<type E: Enum<E>>(explicit complementing enumSet: EnumSet<+E>) -> EnumSet<E>
		= EnumSet.complementOf(enumSet)
}
