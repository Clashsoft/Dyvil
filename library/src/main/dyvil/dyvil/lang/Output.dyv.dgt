// GEN_NOTICE
// Timestamp: TIME_STAMP

package dyvil.lang

import const java.lang.System.out
import func dyvil.array.ObjectArray.arrayToString

abstract class Output {
	// =============== Contructors ===============

	private init() {} // no instances

	// =============== Static Methods ===============

	// --------------- println ---------------

	static func println() -> void inline = out.println()

	// --------------- print ---------------

	static func print(value: any!) -> void inline = out.println(value)

	// overloads for primitives, for performance

	#for (spec <- LibraryGenConstants.BASIC_SPECS) {
		#let(PRIMITIVE = spec['PRIMITIVE'])

		static func print(value: PRIMITIVE) -> void inline = out.println(value)
	}

	static func print(value: String) -> void inline = out.println(value)

	// overloads for arrays, for readable formatting

	#for (spec <- LibraryGenConstants.PRIMITIVE_SPECS) {
		#let(PRIMITIVE = spec['PRIMITIVE'])

		static func print(array: [PRIMITIVE]) -> void inline = out.println(array.toString)
	}

	static func print(array: [Object!]) -> void inline = out.println(array.toString)

	// --------------- print with terminator ---------------

	static func print(value: any!, explicit terminator: String) inline -> void {
		out.print(value)
		out.print(terminator)
	}

	// --------------- print with multiple elements ---------------

	@JavaName("printAll")
	static func print(values: any! ...) -> void {
		printAllImpl(values, ' ')
		println
	}

	@JavaName("printAllWithSeparator")
	static func print(values: any! ..., explicit separator: String) -> void {
		printAllImpl(values, separator)
		println
	}

	@JavaName("printAllWithTerminator")
	static func print(values: any! ..., explicit terminator: String) -> void {
		printAllImpl(values, ' ')
		print terminator
	}

	@JavaName("printAll")
	static func print(values: any! ..., explicit separator: String, explicit terminator: String) -> void {
		printAllImpl(values, separator)
		print terminator
	}

	private static func printAllImpl(values: [any!], separator: String) -> void {
		if values.isEmpty {
			return
		}
		out.print(values[0].arrayToString)
		for i <- 1 ..< values.size {
			out.print(separator)
			out.print(values[i].arrayToString)
		}
	}
}
