package dyvil.tools.dpf.ast

import dyvil.collection.Collection
import dyvil.collection.Map
import dyvil.tuple.Tuple2

public interface Expandable
{
	Object expand(Map<String, Object> mappings, boolean mutate)

	infix func expand(Object obj, Map<String, Object> mappings, boolean mutate): Object
	{
		if (obj is Expandable) return (obj as Expandable).expand(mappings, mutate)
		if (obj is Collection) return expand(obj as Collection<_>, mappings, mutate)
		if (obj is Map) return expand(obj as Map<_, _>, mappings, mutate)
		return obj
	}

	infix func expand(Collection<_> collection, Map<String, Object> mappings, boolean mutate): Collection<_>
	{
		if (!mutate || collection.isImmutable())
		{
			return collection.mapped(e => expand(e, mappings, mutate))
		}
		collection.map(e => expand(e, mappings, true))
		return collection
	}

	infix func expand(Map<_, _> map, Map<String, Object> mappings, boolean mutate): Map<_, _>
	{
		if (!mutate || map.isImmutable())
		{
			return map.entryMapped((k, v) => (expand(k, mappings, mutate), expand(v, mappings, mutate)))
		}
		map.mapEntries((k, v) => (expand(k, mappings, true), expand(v, mappings, true)))
		return map
	}
}
