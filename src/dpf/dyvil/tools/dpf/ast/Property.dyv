package dyvil.tools.dpf.ast

import dyvil.tools.dpf.ast.value.Value
import dyvil.tools.dpf.converter.DPFValueVisitor
import dyvil.tools.dpf.visitor.NodeVisitor
import dyvil.tools.dpf.model.Expandable
import dyvil.tools.parsing.ASTNode
import dyvil.source.position.SourcePosition

public class Property
(
	protected let name:  Name   { public override get }
	protected var value: Value!
)
extends DPFValueVisitor implements dyvil.tools.dpf.model.Property, NodeElement
{
	public init(name: Name) : this(name, null)

	override func value() -> Value = this.value!

	protected override func visitValue(value: Value) -> void = this.value = value

	override func accept(visitor: NodeVisitor) -> void = this.value.accept(visitor.visitProperty(this.name))

	override func expand(mappings: Map<String, Object>, mutate: boolean) -> Property
	{
		let property = mutate ? this : new Property(this.name)
		property.value = Value.wrap(Expandable.expand(this.value, mappings, mutate))
		return property
	}

	override func toString() -> String = String(this)

	override func toString(indent: String, buffer: StringBuilder) -> void
	{
		buffer.append(this.name).append(" = ")
		this.value.toString(indent, buffer)
	}

	override func equals(obj: any!) -> boolean
	{
		if (obj === this) return true
		if (!(obj is Property)) return false

		let that = obj as Property
		return this.name === that.name && this.value == that.value
	}

	override func hashCode() -> int = 31 * this.name.hashCode + this.value.hashCode
}
