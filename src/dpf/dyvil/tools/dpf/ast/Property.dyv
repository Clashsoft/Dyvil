package dyvil.tools.dpf.ast

import dyvil.collection.Map
import dyvil.tools.dpf.ast.value.Value
import dyvil.tools.dpf.converter.DPFValueVisitor
import dyvil.tools.dpf.visitor.NodeVisitor
import dyvil.tools.parsing.Name
import dyvil.tools.parsing.ast.IASTNode
import dyvil.tools.parsing.position.ICodePosition

public class Property
(
	protected Name!  name  { public override get; public set }
	protected Value! value { public get;          public set }
) extends DPFValueVisitor implements NodeElement, Expandable
{
	public init(Name name)
	{
		this.name = name
	}

	protected override func visitValue(Value value) -> void
	{
		this.value = value
	}
	
	override func accept(NodeVisitor visitor) -> void
	{
		this.value.accept(visitor.visitProperty(this.name))
	}

	override func expand(Map<String, Object> mappings, boolean mutate) -> Property
	{
		let property = mutate ? this : new Property(this.name)
		property.value = Value.wrap(Expandable.expand(this.value, mappings, mutate))
		return property
	}

	override func toString() -> String
	{
		return IASTNode.toString(this)
	}
	
	override func toString(String indent, StringBuilder buffer) -> void
	{
		buffer.append(this.name).append(" = ")
		this.value.toString(indent, buffer)
	}

	override func equals(Object! obj) -> boolean
	{
		if (obj === this) return true
		if (!(obj is Property)) return false

		let that = obj as Property
		return this.name === that.name && this.value == that.value
	}

	override func hashCode() -> int = 31 * this.name.hashCode + this.value.hashCode
}
