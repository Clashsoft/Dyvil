package dyvil.tools.dpf.ast.value

import dyvil.tools.dpf.DPFConvertible

import dyvil.collection.immutable.ArrayMap

import dyvil.tools.dpf.ast.value._
import dyvil.tools.dpf.visitor.ValueVisitor
import dyvil.tools.parsing.Name
import dyvil.tools.parsing.ast.IASTNode
import dyvil.tools.parsing.position.ICodePosition

import java.lang.reflect.Array

@LiteralConvertible.FromInt
@LiteralConvertible.FromLong
@LiteralConvertible.FromFloat
@LiteralConvertible.FromDouble
@LiteralConvertible.FromChar
@LiteralConvertible.FromString
@LiteralConvertible.FromBoolean
@LiteralConvertible.FromStringInterpolation
@LiteralConvertible.FromArray
@LiteralConvertible.FromMap
public interface Value extends IASTNode, dyvil.tools.dpf.model.value.Value
{
	// Conversion Methods

	static final func apply(value: Object) -> Value = wrap(value)

	static final func apply(literal: int) -> IntValue = IntValue(literal)

	static final func apply(literal: long) -> LongValue = LongValue(literal)

	static final func apply(literal: float) -> FloatValue = FloatValue(literal)

	static final func apply(literal: double) -> DoubleValue = DoubleValue(literal)

	static final func apply(literal: char) -> StringValue = StringValue(literal.toString)

	static final func apply(literal: String) -> StringValue = StringValue(literal)

	static final func apply(literal: Name) -> NameAccess = NameAccess(literal)

	static final func apply(literal: boolean) -> NameAccess = NameAccess(Name.fromRaw(literal.toString))

	static final func apply(elements: Value...) -> ListValue = ListValue(elements)

	static final func apply(keys: Value..., values: Value...) -> MapValue = new MapValue(new ArrayMap(keys, values))

	static final func apply(base: String, values: Value...) -> StringInterpolation
		= new StringInterpolation(base.split(@"\\\d+") as [String], values)

	static final func apply(base: String, values: Object...) -> StringInterpolation
		= new StringInterpolation(base.split(@"\\\d+") as [String], values)

	// Override Methods

	override func getPosition() -> ICodePosition! = null

	override func setPosition(position: ICodePosition!) -> void { }

	override func toString(indent: String, buffer: StringBuilder) -> void

	override func accept(visitor: ValueVisitor) -> void

	// Value Conversions

	static final func wrap(value: any!) -> Value
	{
		if (value is Value) return value as Value

		let valueClass = value.dynamicClass

		if (valueClass === class String) return new StringValue(value as String)
		if (valueClass === class Int) return new IntValue(value as Int)
		if (valueClass === class Long) return new LongValue(value as Long)
		if (valueClass === class Float) return new FloatValue(value as Float)
		if (valueClass === class Double) return new DoubleValue(value as Double)
		if (valueClass === class Name) return NameAccess(value as Name)
		if (valueClass === class Boolean) return Value(value as boolean)

		if (value is DPFConvertible) return (value as DPFConvertible).toPropertyValue()
		if (value is Collection) return ListValue(value as Collection)
		if (value is Map) return MapValue(value as Map)
		if (value.getClass.isArray)
		{
			// Convert Arrays using Reflection
			let size = Array.getLength(value)
			let listValue = ListValue(size)

			for (i <- 0 ..< size)
			{
				listValue.add(value: wrap(Array.get(value, i)))
			}

			return listValue
		}

		throw new RuntimeException("Cannot convert instance of type \(valueClass) to a DPF Value")
	}
}
