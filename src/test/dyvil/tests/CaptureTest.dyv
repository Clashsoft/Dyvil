package dyvil.tests

using dyvil.Test

class CaptureTest
{
	static func apply() -> Test = test "Capture" {
		lambdaCapture()
		anonClassCapture()
	}

	static func supplier(i: int) -> (-> int) = => i

	static func classOf<@Reified(.ANY_CLASS) type T>() -> () -> Class<T> = => class T

	static func typeOf<@Reified(.TYPE) type T>() -> () -> Type<T> = => type T

	private static func lambdaCapture() -> Test = test "Lambda" {
		let abc = -1
		let fun = (i: int) => (j: int) => (k: int) => (i, j, k, abc)
		let res = fun 1 2 3

		res =?= (1, 2, 3, -1)

		test "Method Parameter Capture" {
			let supplier = supplier(10)
			supplier() =?= 10
		}

		// Lambda Parameter capture by reference
		test "Lambda Parameter Capture" {
			let supplierLambda = (i: int) => => i
			let supplier = supplierLambda(10)
			supplier() =?= 10
		}

		test "Type Parameter Capture" {
			let supplier = classOf<String>()
			supplier() =?= class<String>
		}

		lambdaThisCapture()
	}

	static class ThisCapture
	{
		func anonymous() -> Object = new Object {
			override func toString() -> String = this<ThisCapture>.toString
		}

		func lambda() -> () -> ThisCapture = => this

		override func toString() -> String = System.identityHashCode(this).toHexString
	}

	private static func lambdaThisCapture() -> Test = test "This Capture" {
		let instance = new ThisCapture
		let lambda = instance.lambda

		lambda() =?= instance
	}

	private static func anonClassCapture() -> Test = test "Anonymous Classes" {
		var abc = 123
		var obj = new Object {
			override func toString() -> String = abc.toString
		}

		obj.toString =?= abc.toString

		anonClassThisCapture()
	}

	private static func anonClassThisCapture() -> Test = test "This Capture" {
		var instance = new ThisCapture
		var obj = instance.anonymous

		instance.toString =?= obj.toString
	}
}
