package dyvil.tests

using dyvil.Test

public class VirtualStaticTest
{
	// Static Virtual Method Test

	public class Base
	{
		public static func foo(): String = "Base"
		public static func bar(String s): String = s + "_Base"
	}

	public class Derived extends Base
	{
		public static func foo(): String = "Derived"
		public static func bar(String s): String = s + "_Derived"
	}

	public static func getFoo<@Reified(Reified.Type.ANY_CLASS) T: Base>(): String = T.foo
	public static func getBar<@Reified(Reified.Type.OBJECT_CLASS) T: Base>(String s): String = T.bar(s)

	interface IntConstructible
	{
		static func apply<T>(int i): T = throw new AbstractMethodError
	}

	case class CustomInt(public let value: int) implements IntConstructible
	case class MyInt    (public let value: int) implements IntConstructible

	public static func create<@Reified(Reified.Type.TYPE) T: IntConstructible>(int i): T = T<T>(i)

	public static func apply(): Test = test "Virtual Static Methods" {
		getFoo<Base> =?= "Base"
		getFoo<Derived> =?= "Derived"
		getBar<Base>("") =?= "_Base"
		getBar<Derived>("") =?= "_Derived"

		let customInt = create<CustomInt>(10)
		let myInt = create<MyInt>(20)

		customInt.value =?= 10
		myInt.value =?= 20
	}
}
