package dyvil.tests

using dyvil.Test

class BridgeTest
{
	static interface Container<T>
	{
		func get() -> T

		func set(value: T) -> void
	}

	static case class IntContainer(value: int) implements Container<int>
	{
		override func get() -> int = this.value

		override func set(value: int) -> void = this.value = value
	}

	static case class LongContainer(value: long) implements Container<long>
	{
		override func get() -> long = this.value

		override func set(value: long) -> void = this.value = value
	}

	static func apply() -> Test = test "Bridge Methods" {
		let ic = IntContainer(1)

		test "IntContainer" { ic.get =?= 1 }

		let ci: Container<int> = ic
		ic.set(3)

		test "Container<int>" { ci.get =?= 3 }

		let lc = LongContainer(2L)

		test "LongContainer" { lc.get =?= 2L }

		let cl: Container<long> = lc
		lc.set(4L)

		test "Container<long>" { cl.get =?= 4L }
	}
}
