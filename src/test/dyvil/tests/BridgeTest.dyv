package dyvil.tests

using dyvil.Test

public class BridgeTest
{
	public interface Container<T>
	{
		func get(): T

		func set(T value): void
	}

	public case class IntContainer(int value) implements Container<int>
	{
		public override func get(): int = this.value

		public override func set(int value): void = this.value = value
	}

	public case class LongContainer(long value) implements Container<long>
	{
		public override func get(): long = this.value

		public override func set(long value): void = this.value = value
	}

	public static func apply(): Test = test "Bridge Methods" {
		let ic = IntContainer(1)

		test "IntContainer" { ic.get =?= 1 }

		let ci: Container<int> = ic
		ic.set(3)

		test "Container<int>" { ci.get =?= 3 }

		let lc = LongContainer(2L)

		test "LongContainer" { lc.get =?= 2L }

		let cl: Container<long> = lc
		lc.set(4L)

		test "Container<long>" { cl.get =?= 4L }
	}
}
