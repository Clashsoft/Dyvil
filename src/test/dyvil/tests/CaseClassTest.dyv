package dyvil.tests

using dyvil.Test

import java.io._

public class CaseClassTest
{
	public case class Person(let id: long, let name: String, let age: int, let height: double)

	public static func apply(): Test = test "Case Classes" {
		let id     = 1234567890L
		let name   = "Peter Peterson"
		let age    = 20
		let height = 1.92
		let person = Person(id, name, age, height)
		let other  = Person(id, name, age, height)
		let string = "Person(\(id), \(name), \(age), \(height))"

		test "Equality"  { person          =?= other }
		test "Hash Code" { person.hashCode =?= other.hashCode }
		test "String"    {
			person.toString =?= other.toString
			person.toString =?= string
		}

		test "Serialization" {
			// ---- Serialize ----

			let bos = new ByteArrayOutputStream
			let oos = new ObjectOutputStream(bos)

			oos.writeObject(person)
			oos.close()

			let bytes = bos.toByteArray

			// ---- Deserialize ----

			let ois = new ObjectInputStream(new ByteArrayInputStream(bytes))
			let deserialized = ois.readObject

			deserialized =?= person
		}
	}
}
