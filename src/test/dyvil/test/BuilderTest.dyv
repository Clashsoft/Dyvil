package dyvil.test

public class BuilderTest
{
	public case class Buildable(private int _int
                                private double _double
                                private String _string)
    {
		public static Buildable apply() = new Buildable(0, 0D, null)

		public static Buildable apply(extension Buildable => void configure)
		{
			return Buildable().{
				configure $0
				$0
			}
		}

		public int myInt
		{
			get: return this._int
			set: this._int = newValue
		}

		public double myDouble
		{
			get: return this._double
			set: this._double = newValue
		}

		public String myString
		{
			get: return this._string
			set: this._string = newValue
		}
    }

	public static void main([String] args)
	{
		testInline()
		testClosure()
	}

	private static void testInline()
	{
		var buildable = Buildable().{
			myInt = -42
			myDouble = 2.5D
			myString = "Test"
			$0
		}

		Main.test("Builder Inline Test", buildable, Buildable(-42, 2.5D, "Test"))
	}

	private static void testClosure()
	{
		var buildable = Buildable {
			myInt = 10
			myString = "Hello World"
			myDouble = 1.1D
		}

		Main.test("Builder Closure Test", buildable, Buildable(10, 1.1D, "Hello World"))
	}
}
