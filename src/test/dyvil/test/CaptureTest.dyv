package dyvil.test

public class CaptureTest
{
	public static void main([String] args) {
		println "-- Lambda Capture --"
		lambdaCapture()
		println "-- Lambda This Capture"
		lambdaThisCapture()
		println "-- Anonymous Class Capture --"
		anonClassCapture()
		println "-- Anonymous Class This Capture --"
		anonClassThisCapture()
	}
	
	private static void lambdaCapture() {
		int abc = -1
		auto fun = (int i) => (int j) => (int k) => (i, j, k, abc)
		auto res = fun(1)(2)(3)
		println "Expected: (1, 2, 3, -1), Actual: " + res
	}
	
	private class ThisCapture {
		public Object anonymous() = new Object {
			public override String toString() = this[ThisCapture].toString
		}
		
		public => ThisCapture lambda() = => this
	}
	
	private static void lambdaThisCapture() {
		auto instance = new ThisCapture
		auto lambda = instance.lambda
		auto res = lambda()
		println "Expected: " + instance + ", Actual: " + res
	}
	
	private static void anonClassCapture() {
		int abc = 123
		auto obj = new Object {
			public override String toString() = this.abc.toString
		}
		
		println "Expected: " + abc + ", Actual: " + obj
	}
	
	private static void anonClassThisCapture() {
		auto instance = new ThisCapture
		auto obj = instance.anonymous
		println "Expected: " + instance + ", Actual: " + obj
	}
}