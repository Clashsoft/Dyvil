package dyvil.test

public class LambdaTest
{	
	functional interface Function[P, R]
	{
		public R apply(P par1)
		
		public Function[P, A] andThen[A](Function[R, A] other) = x => other(this(x))
	}
	
	functional interface IntFunction extends Function[int, int] {
		public override int apply(int value)
	}
	
	private static int staticSum = 0
	
	public static void main([String] args)
	{
		println "-- Basic Lambda Test --"
		basicTest()
		println "-- Function Chain Test --"
		chainTest()
		println "-- Closure Test --"
		closureTest()
		println "-- Compound Assignment Test --"
		compoundAssignmentTest()
		println "-- Custom SAM Interface Test --"
		customSAMTest()
		println "-- Partial Function Test"
		partialFunctionTest()
	}
	
	private static void basicTest() {
		auto map = [ 1 : "a", 2 : "b" ].flatMapped((i, s) => [ (i, s.toUpperCase), (i * 10, s) ])
		println map
	}
	
	private static void chainTest() {
		auto fun1 = (int i) => i + 1
		auto fun2 = (int i) => i * 2
		auto fun12 = fun1 andThen fun2
		println(fun12(7))
	}
	
	private static void compoundAssignmentTest() {
		int localSum = 0
		auto list = List(1, 2, 3)
		list.forEach(i => localSum += i)
		list.forEach(i => staticSum += i)
		println localSum
		println staticSum
	}
	
	private static void customSAMTest() {
		IntFunction intF = i => i * i
		println(intF(7))
				
		auto fun1 = intF
		println(fun1(10))
	}

	private static void closureTest() {
		() => void function1 = function { println "Successful" }
		() => String function2 = function { "Testing..." }

		repeat(3) { println(function2()) }

		with ("Still testing...") { println $0 }

		function1()
	}

	private static void partialFunctionTest() {
		(int, int) => int compareInts = _ compareTo _;
		int => void printFun = println _

		printFun(compareInts(1, 2))

		// with ("Test running...") { println _ }
		with ("Test completed", println _)
	}
}
