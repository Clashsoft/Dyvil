package dyvil.test

import	dyvil.annotation.Intrinsic
using	dyvil.reflect.Opcodes._

infix operator +- { precedence 100, associativity left }

/**
 * A test for annotations and intrinsic methods
 */
public class IntrinsicTest
{
	private static int value

	public static void main([String] args)
	{
		var i1 = 1 +- 2
		var i2 = 2 +- -4
		var i3 = -1 +- 3
		var i4 = -7 +- -5

		Main.test("Intrinsic Test 1", -1, i1)
		Main.test("Intrinsic Test 2", 6, i2)
		Main.test("Intrinsic Test 3", -4, i3)
		Main.test("Intrinsic Test 4", -2, i4)
		
		setValue 1
		setValue 7

		Main.test("Special Intrinsic Test", 7, value)
	}
	
	@Intrinsic([ LOAD_0, LOAD_1, INEG, IADD ])
	public infix func +-(int i, int j): int = throw new RuntimeException("Not Intrinsic!")
	
	@Intrinsic(value: [ LOAD_0, PUTSTATIC, 0, 1, 2 ], strings: [ "dyvil/test/IntrinsicTest", "value", "I" ])
	private static void setValue(int value) = throw new RuntimeException("Not Intrinsic!")
}
