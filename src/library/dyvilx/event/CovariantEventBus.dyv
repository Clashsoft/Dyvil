package dyvilx.event

class CovariantEventBus extends InvariantEventBus
{
	override func dispatch(event: Object) -> void
	{
		let eventType = event.dynamicClass

		for (entry <- this.handlers)
		{
			this.invokeRecursively(entry, entry.handler, event, eventType)
		}
	}

	private func invokeRecursively(entry: HandlerEntry, handler: Object!, event: Object, eventType: Class<_>) -> void
	{
		let handlerMethod = entry.getHandler(eventType)
		if (handlerMethod != null)
		{
			this.invoke(handlerMethod!!, handler, event)
		}

		let superClass: Class<_>? = eventType.getSuperclass()
		if (superClass != null)
		{
			this.invokeRecursively(entry, handler, event, superClass!!)
		}

		for (itf: Class<_>? <- eventType.getInterfaces())
		{
			this.invokeRecursively(entry, handler, event, itf!!)
		}
	}
}
