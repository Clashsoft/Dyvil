package dyvilx.event

class CovariantEventBus extends InvariantEventBus
{
	override func dispatch(event: Object) -> void
	{
		let eventType = event.dynamicClass

		for (entry <- this.handlers)
		{
			this.invokeRecursively(entry, entry.handler, event, eventType)
		}
	}

	private func invokeRecursively(entry: HandlerEntry, handler: Object!, event: Object, eventType: Class<_>) -> void
	{
		if let handlerMethod = entry.getHandler(eventType)
		{
			this.invoke(handlerMethod, handler, event)
		}

		if let superClass = eventType.getSuperclass()
		{
			this.invokeRecursively(entry, handler, event, superClass)
		}

		for (itf <- eventType.getInterfaces())
		{
			this.invokeRecursively(entry, handler, event, itf!!)
		}
	}
}
