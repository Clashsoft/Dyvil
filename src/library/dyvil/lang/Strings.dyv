package dyvil.lang

import dyvil.collection.range.specialized.IntRange
import java.util.regex.{ Pattern, Matcher }

@JavaName("Strings")
extension String {
	// =============== Operators ===============

	// --------------- Pattern Matching ---------------

	// here so the compiler does not use the Pattern-based method with an implicit conversion
	infix func ~=(pattern: String, value: String) = pattern == value

	infix func ~=(pattern: Pattern, value: String) = pattern.matches(value)

	// --------------- Concatenation ---------------

	infix func +(lhs: char, rhs: String) -> String @Intrinsic([], compilerCode: Intrinsic.STRING_CONCAT) = lhs + rhs

	infix func +(lhs: String, rhs: char) -> String @Intrinsic([], compilerCode: Intrinsic.STRING_CONCAT) = lhs + rhs

	infix func ++(lhs: String, rhs: String) -> String @Intrinsic([], compilerCode: Intrinsic.STRING_CONCAT) = lhs ++ rhs

	// --------------- Repetition ---------------

	infix func *(lhs: int, rhs: String) -> String inline = rhs.repeated(lhs)

	infix func *(lhs: String, rhs: int) -> String inline = lhs.repeated(rhs)

	// =============== Static Methods ===============

	static func apply(from value: any!) -> String inline = "\(value)"

	static func apply(from sequence: CharSequence) implicit -> String inline = sequence.toString()

	@JavaName("build")
	static func apply(builtBy closure: StringBuilder.() -> void) -> String inline
		= new StringBuilder().use(closure).toString!!

	@JavaName("build")
	static func apply(capacity: int, builtBy closure: StringBuilder.() -> void) -> String inline
		= new StringBuilder(capacity).use(closure).toString!!

	@JavaName("fromShared")
	@java.lang.Deprecated // moved to UnsafeString
	static func __apply(sharing array: [final char]) -> String = String(sharing: array)

	// =============== Methods ===============

	@Deprecated(description: "use String.charCount", since: "0.45.0", forRemoval: "v0.46.0",
	            replacements: [ "String.charCount" ])
	@java.lang.Deprecated // use String.length
	final func size() -> int inline = this.length

	// --------------- Char Access ---------------

	final func subscript(index: int) -> char inline = this.charAt(index)

	final func subscript(range: Range<int>) -> String inline = this.substring(range.first, range.last + 1)!!

	@JavaName("getCharArray")
	@java.lang.Deprecated // moved to UnsafeStrings
	final func __getCharArray() -> [final char] = this.getCharArray()

	@JavaName("contains")
	@java.lang.Deprecated // moved to StringChars
	final func __contains(character: char) -> boolean = this.contains(character)

	@JavaName("count")
	@java.lang.Deprecated // moved to StringChars
	final func __count(character: char) -> int = this.count(character)

	@JavaName("split")
	@java.lang.Deprecated // moved to StringChars
	final func __split(by delimiter: char) -> [String] = this.split(by: delimiter)

	// --------------- Misc. ---------------

	final func format(args: Object! ...) -> String inline = String.format(this, args...)!!

	final func count(substring: String) -> int = this.count(substring, 0, this.length)

	final func count(substring: String, from: int, to: int) -> int {
		let subLen = substring.length
		var count = 0
		var i = from
		while 0 <= (i = this.indexOf(substring, i)) && i + subLen <= to {
			count += 1
			i += subLen
		}
		return count
	}

	final func split(explicit by delimiter: String) -> [String] {
		if this.isEmpty {
			// special case: "".split(anything) is always [""]
			return [""]
		}
		if delimiter.isEmpty {
			// special case: anything.split("") returns the characters as strings of width 1
			return this.charGroups(width: 1)
		}

		let delimiterLength = delimiter.length
		let upperBound = this.length _/ delimiterLength + 1
		let array = new [String](upperBound)

		var start = 0

		for i <- 0 ..< upperBound {
			let index = this.indexOf(delimiter, start)
			if index < 0 {
				array[i] = this.substring(start)!!
				break
			}
			else {
				array[i] = this.substring(start, index)!!
			}
			start = index + delimiterLength
		}

		// trim trailing empty and null elements
		var end = array.size
		while end > 0 && (array[end - 1] == null || array[end - 1].isEmpty) {
			end -= 1
		}
		return array.trim(end)
	}

	final func distance(to that: String) -> int {
		if this == that {
			return 0
		}

		let size1 = this.length
		let size2 = that.length

		if size1 == 0 {
			return size2
		}
		if size2 == 0 {
			return size1
		}

		let arraySize = size2 + 1
		let array1 = new [int](arraySize)
		let array2 = new [int](arraySize)

		for i <- 0 ..< arraySize {
			array1[i] = i
		}
		for i <- 0 ..< size1 {
			array2[0] = i + 1
			for j <- 0 ..< size2 {
				let offset = (this[i] != that[j]) ? 1 : 0
				array2[j + 1] = Math.min(array2[j] + 1, Math.min(array1[j + 1] + 1, array1[j] + offset))
			}
			System.arraycopy(array2, 0, array1, 0, arraySize)
		}
		return array2[size2]
	}

	final func replaceAll(regex: String, by replacer: String -> String) -> String inline
		= this.replaceAll(Pattern(from: regex), replacer)

	final func replaceAll(pattern: Pattern, by replacer: String -> String) -> String {
		let matcher = pattern.matcher(this)!!
		let buffer = new StringBuffer

		while matcher.find() {
			matcher.appendReplacement(buffer, replacer(matcher.group()!!))
		}

		matcher.appendTail(buffer)
		return buffer.toString!!
	}

	@JavaName("repeat")
	final func repeated(times: int) -> String = times match {
		case 0 => return ""
		case 1 => return this
		case 2 => return this.concat(this)!!
		case _ => String(this.length * times) { for i <- 0 ..< times { $0.append(this) } }
	}
}

@JavaName("UnsafeString")
extension String {
	// =============== Constants ===============

	private const STRING_VALUE_OFFSET: long
		= dyvil.reflect.ReflectUtils.UNSAFE.objectFieldOffset(class<String>.getDeclaredField("value"))

	// =============== Static Methods ===============

	@JavaName("fromShared")
	static func apply(sharing array: [final char]) -> String
		= dyvil.reflect.ReflectUtils.JAVA_LANG_ACCESS.newStringUnsafe(array)!!

	// =============== Methods ===============

	final func getCharArray() -> [final char]
		= dyvil.reflect.ReflectUtils.UNSAFE.getObject(this, STRING_VALUE_OFFSET) as [final char]
}

@JavaName("StringChars")
extension String {
	// =============== Methods ===============

	final func charCount() -> int inline = this.length

	final func contains(character: char) -> boolean inline = this.indexOf(character) >= 0

	final func count(character: char) -> int = this.count(character, from: 0, to: this.length)

	final func count(character: char, from: int, to: int) -> int {
		var count = 0
		for i <- from ..< to {
			if this.charAt(i) == character {
				count += 1
			}
		}
		return count
	}

	final func split(by delimiter: char) -> [String] {
		if this.isEmpty {
			// special case: "".split(anything) is always [""]
			return [""]
		}

		// String.split(String) spec says trailing "" in result are to be omitted,
		// which is the same as omitting trailing delimiters.
		var end = this.length
		while end > 0 && this.charAt(end - 1) == delimiter {
			end -= 1
		}

		if end == 0 {
			// only consists of the delimiter
			return []
		}

		let count = this.count(delimiter, from: 0, to: end)

		// skip the loop if the delimiter only occurs trailing or is not in the string at all
		if count == 0 {
			return [this.substring(0, end)!!]
		}

		let array = new [String](count + 1)
		var start = 0
		var arrayIndex = 0

		for i <- 0 ..< end {
			if this[i] == delimiter {
				array[arrayIndex] = this.substring(start, i)!!
				arrayIndex += 1
				start = i + 1
			}
		}
		array[arrayIndex] = this.substring(start, end)!!
		return array
	}

	final func charGroups(width: int) -> [String] {
		if this.isEmpty {
			// special case: "".charGroups(anything) is always []
			// also the only case where count == 0, which causes problems in the second to last line
			return []
		}

		let count = (this.length + width - 1) _/ width
		let array = new [String](count)

		for i <- 0 ..< count - 1 {
			array[i] = this.substring(i * width, (i + 1) * width)
		}

		array[count - 1] = this.substring((count - 1) * width)
		return array
	}
}

@JavaName("StringCodePoints")
extension String {
	// =============== Methods ===============

	final func codePointCount() -> int inline = this.codePointCount(0, this.length)

	final func contains(codePoint: int) -> boolean inline = this.indexOf(codePoint) >= 0

	final func count(codePoint: int) -> int = this.count(codePoint, from: 0, to: this.length)

	final func count(codePoint: int, from: int, to: int) -> int {
		var count = 0
		var i = from
		while i < to {
			let code = this.codePointAt(i)
			if code == codePoint {
				count += 1
			}
			i += Character.charCount(code)
		}
		return count
	}

	final func split(by delimiter: int) -> [String] {
		let count = this.count(delimiter)
		if count == 0 {
			return [this]
		}

		let array = new [String](count + 1)
		var start = 0
		var arrayIndex = 0
		var i = 0
		let len = this.length

		while i < len {
			let code = this.codePointAt(i)
			if code == delimiter {
				array[arrayIndex] = this.substring(start, i)!!
				arrayIndex += 1
				start = i += Character.charCount(code)
			}
		}
		array[arrayIndex] = this.substring(start)!!
		return array
	}
}

@JavaName("CharSequences")
extension CharSequence {
	// =============== Operators ===============

	static func |_|(_ s: CharSequence) -> int inline = s.length

	// =============== Methods ===============

	final func first() -> char inline = this.charAt(0)

	final func last() -> char inline = this.charAt(this.length - 1)

	final func indices() -> IntRange inline = 0 ..< this.length

	final func subscript(index: int) -> char inline = this.charAt(index)

	final func subscript(range: Range<int>) -> CharSequence inline = this.subSequence(range.first, range.last + 1)
}

@JavaName("StringBuilders")
extension StringBuilder {
	// =============== Methods ===============

	final func remove(at index: int) -> StringBuilder {
		this.deleteCharAt(index)
		return this
	}

	final func removeFirst() -> StringBuilder = this.remove(at: 0)

	final func removeLast() -> StringBuilder = this.remove(at: this.length - 1)
}

@JavaName("Patterns")
extension Pattern {
	// =============== Static Methods ===============

	static func apply(from string: String) implicit -> Pattern inline = Pattern.compile(string)!!

	// =============== Methods ===============

	final func matches(string: String) -> boolean = this.matcher(string).matches()
}

// The following classes only have syntactic meaning in the Dyvil language,
// they are practically useless in Java code.

@JavaName("Strings$CharSyntax")
extension char {
	// =============== Static Methods ===============

	static func apply(_ c: char) -> char inline = c

	/**
	 * This method only exists to provide the user with a useful warning message when trying to pass an invalid char
	 * literal to the `char` method. An invalid char literal would be a double-quoted String or a single-quoted String
	 * with zero or more than one characters.
	 */
	@UsageInfo(value: "Multi-character Literal or String passed to 'char' method", level: .WARNING)
	static func apply(_ c: String) -> char inline = c[0]
}

@JavaName("Strings$AnyToStringBuilder")
extension any {
	// =============== Methods ===============

	func toString(builder: StringBuilder) -> void inline = builder.append(this)
}
