// GEN_NOTICE
// Timestamp: TIME_STAMP

package dyvil.lang

import dyvil.util.AutoPrinter
import java.io.PrintStream

import const java.lang.System.out
import func dyvil.array.ObjectArray.deepToString

public abstract class Output
{
	private init()
	{
		// no instances
	}

	// C++ style output

	public const ENDL: String = System.lineSeparator

	infix inline func <<(stream: PrintStream, value: any) -> PrintStream = stream.{ print value; $0 }

	// println functions

	static inline func println() -> void = out.println()

	// byte and short specializations are omitted
#define LIGHT_PRIMITIVE_SPECS Base-Boolean.dgs, Base-Char.dgs, Base-Int.dgs, Base-Long.dgs, Base-Float.dgs, Base-Double.dgs
#foreach LIGHT_PRIMITIVE_SPECS

	@Deprecated(replacements: [ "print(PRIMITIVE) -> void" ])
	static inline func println(value: PRIMITIVE) -> void = out.println(value)
#end

	@Deprecated(replacements: [ "print(AutoPrinter.() -> void) -> void" ])
	static inline func println(closure: AutoPrinter.() -> void) -> void = closure(AutoPrinter)

	@Deprecated(replacements: [ "print(String) -> void" ])
	static inline func println(value: String) -> void = out.println(value)

	@Deprecated(replacements: [ "print(Object!) -> void" ])
	static inline func println(value: Object!) -> void = out.println(value)

#import Base.dgs
#foreach PRIMITIVE_SPECS
	@Deprecated(replacements: [ "print([PRIMITIVE]) -> void" ])
	static inline func println(array: [PRIMITIVE]) -> void = out.println(array.toString)

#end
	@Deprecated(replacements: [ "print([Object!]) -> void" ])
	static inline func println(array: [Object!]) -> void = out.println(array.deepToString)

	// print functions

#foreach LIGHT_PRIMITIVE_SPECS
	static inline func print(value: PRIMITIVE) -> void = out.println(value)

#end
	static inline func print(closure: AutoPrinter.() -> void) -> void = closure(AutoPrinter)

	static inline func print(value: String) -> void = out.println(value)

	static inline func print(value: Object!) -> void = out.println(value)

#import Base.dgs
#foreach PRIMITIVE_SPECS
	static inline func print(array: [PRIMITIVE]) -> void = out.println(array.toString)

#end
	static inline func print(array: [Object!]) -> void = out.println(array.deepToString)

	// print(All) functions

	@BytecodeName("printAll")
	static inline func print(values: Object! ...) -> void
	{
		print(values..., end: '\n')
	}

	@BytecodeName("printAll_separator")
	static inline func print(values: Object! ..., explicit separator: String) -> void
	{
		print(values..., separator: separator, end: ENDL)
	}

	@BytecodeName("printAll_end")
	static inline func print(values: Object! ..., explicit end: String) -> void
	{
		print(values..., separator: ' ', end: end)
	}

	@BytecodeName("printAll")
	static func print(values: Object! ..., explicit separator: String, explicit end: String) -> void
	{
		if !values.isEmpty
		{
			out.print(values[0].deepToString)
			for i <- 1 ..< values.size
			{
				out.print(separator)
				out.print(values[i].deepToString)
			}
		}

		out.print(end)
	}
}
