package dyvil.collection

import java.util.{ Map, SortedMap, NavigableMap }
import java.util.{ HashMap, LinkedHashMap, IdentityHashMap, WeakHashMap, TreeMap, EnumMap }
import java.util.Collections
import java.util.Comparator

import dyvil.ref.ObjectRef

// *************** Map ***************

@JavaName("Maps")
extension<type K, type V> Map<K, V> {
	// =============== Static Methods ===============

	@JavaName("empty")
	static func apply<type K, type V>() -> @Immutable Map<K, V> = Collections.emptyMap<K, V>()

	// static func apply<type K, type V>(key: K, value: V) -> @Immutable Map<K, V> = Collections.singletonMap(key, value)

	// static func apply<type K, type V>(tuple: (K, V)) -> @Immutable Map<K, V> = Collections.singletonMap(tuple._1, tuple._2)

	@JavaName("copyOf")
	static func apply<type K, type V>(withContentsOf map: Map<+K, +V>) -> @Immutable Map<K, V>
		= LinkedHashMap(withContentsOf: map).unmodifiableView

	@JavaName("build")
	static func apply<type K, type V>(builtBy configure: Map<K, V>.() -> void) -> @Immutable Map<K, V> {
		let map = new LinkedHashMap<K, V>()
		configure(map)
		return map.unmodifiableView
	}

	// =============== Methods ===============

	// --------------- Key Subscript ---------------

	func subscript(key: K) -> V! = this.get(key)

	@Mutating
	func subscript_=(key: K, newValue: V) -> void = this.put(key, newValue)

	@Mutating
	func subscript_&(key: K) -> V* = new ObjectRef<V>() {
		override func get() -> V = this<Map<K, V>>.get(key)

		override func set(value: V) -> void = this<Map<K, V>>.put(key, value)
	}

	// --------------- Views ---------------

	func synchronizedView() -> Map<K, V> = Collections.synchronizedMap(this)

	func unmodifiableView() -> Map<K, V> = Collections.unmodifiableMap(this)
}

// *************** SortedMap ***************

@JavaName("SortedMaps")
extension<type K, type V> SortedMap<K, V> {
	// =============== Static Methods ===============

	@JavaName("empty")
	static func apply<type K, type V>() -> SortedMap<K, V> = Collections.emptySortedMap<K, V>()

	// =============== Methods ===============

	// --------------- Views ---------------

	func synchronizedView() -> SortedMap<K, V> = Collections.synchronizedSortedMap(this)

	func unmodifiableView() -> SortedMap<K, V> = Collections.unmodifiableSortedMap(this)
}

// *************** NavigableMap ***************

@JavaName("NavigableMaps")
extension<type K, type V> NavigableMap<K, V> {
	// =============== Static Methods ===============

	@JavaName("empty")
	static func apply<type K, type V>() -> NavigableMap<K, V> = Collections.emptyNavigableMap<K, V>()

	// =============== Methods ===============

	// --------------- Views ---------------

	func synchronizedView() -> NavigableMap<K, V> = Collections.synchronizedNavigableMap(this)

	func unmodifiableView() -> NavigableMap<K, V> = Collections.unmodifiableNavigableMap(this)
}

// *************** HashMap ***************

@JavaName("HashMaps")
extension HashMap {
	// =============== Static Methods ===============

	static func apply<type K, type V>() -> HashMap<K, V> = new HashMap<K, V>()

	static func apply<type K, type V>(withCapacity capacity: int) -> HashMap<K, V> = new HashMap<K, V>(capacity)

	static func apply<type K, type V>(withCapacity capacity: int, loadFactor: float) -> HashMap<K, V>
		= new HashMap<K, V>(capacity, loadFactor)

	static func apply<type K, type V>(withContentsOf map: Map<+K, +V>) -> HashMap<K, V> = new HashMap<K, V>(map)
}

// *************** LinkedHashMap ***************

@JavaName("LinkedHashMaps")
extension LinkedHashMap {
	// =============== Static Methods ===============

	static func apply<type K, type V>() -> LinkedHashMap<K, V> = new LinkedHashMap<K, V>()

	static func apply<type K, type V>(withCapacity capacity: int) -> LinkedHashMap<K, V>
		= new LinkedHashMap<K, V>(capacity)

	static func apply<type K, type V>(withCapacity capacity: int, loadFactor: float) -> LinkedHashMap<K, V>
		= new LinkedHashMap<K, V>(capacity, loadFactor)

	static func apply<type K, type V>(withCapacity capacity: int, loadFactor: float, accessOrder: boolean) -> LinkedHashMap<K, V>
		= new LinkedHashMap<K, V>(capacity, loadFactor, accessOrder)

	static func apply<type K, type V>(withContentsOf map: Map<+K, +V>) -> LinkedHashMap<K, V>
		= new LinkedHashMap<K, V>(map)
}

// *************** IdentityHashMap ***************

@JavaName("IdentityHashMaps")
extension IdentityHashMap {
	// =============== Static Methods ===============

	static func apply<type K, type V>() -> IdentityHashMap<K, V> = new IdentityHashMap<K, V>()

	static func apply<type K, type V>(withCapacity capacity: int) -> IdentityHashMap<K, V>
		= new IdentityHashMap<K, V>(capacity)

	static func apply<type K, type V>(withContentsOf map: Map<+K, +V>) -> IdentityHashMap<K, V>
		= new IdentityHashMap<K, V>(map)
}

// *************** WeakHashMap ***************

@JavaName("WeakHashMaps")
extension WeakHashMap {
	// =============== Static Methods ===============

	static func apply<type K, type V>() -> WeakHashMap<K, V> = new WeakHashMap<K, V>()

	static func apply<type K, type V>(withCapacity capacity: int) -> WeakHashMap<K, V> = new WeakHashMap<K, V>(capacity)

	static func apply<type K, type V>(withCapacity capacity: int, loadFactor: float) -> WeakHashMap<K, V>
		= new WeakHashMap<K, V>(capacity, loadFactor)

	static func apply<type K, type V>(withContentsOf map: Map<+K, +V>) -> WeakHashMap<K, V> = new WeakHashMap<K, V>(map)
}

// *************** TreeMap ***************

@JavaName("TreeMaps")
extension TreeMap {
	// =============== Static Methods ===============

	static func apply<type K: Comparable<K>, type V>() -> TreeMap<K, V> = new TreeMap<K, V>()

	static func apply<type K, type V>(comparingBy comparator: Comparator<-K>) -> TreeMap<K, V>
		= new TreeMap<K, V>(comparator)

	static func apply<type K: Comparable<K>, type V>(withContentsOf map: Map<+K, +V>) -> TreeMap<K, V>
		= new TreeMap<K, V>(map)

	static func apply<type K, type V>(withContentsOf map: SortedMap<+K, +V>) -> TreeMap<K, V> = new TreeMap<K, V>(map)
}

// *************** EnumMap ***************

@JavaName("EnumMaps")
extension EnumMap {
	// =============== Static Methods ===============

	static func apply<type K: Enum<K>, type V>(of enumClass: Class<+K>) -> EnumMap<K, V> = new EnumMap<K, V>(enumClass)

	static func apply<type K: Enum<K>, type V>(withContentsOf map: Map<+K, +V>) -> EnumMap<K, V>
		= new EnumMap<K, V>(map)

	static func apply<type K: Enum<K>, type V>(withContentsOf map: EnumMap<+K, +V>) -> EnumMap<K, V>
		= new EnumMap<K, V>(map)
}


