package dyvil.collection

interface Cell<type +R, type +C, type +V> extends java.io.Serializable
{
	/**
	 * Returns the row stored by this cell
	 *
	 * @return the row
	 */
	func getRow() -> R

	/**
	 * Returns the column stored by this cell
	 *
	 * @return the column
	 */
	func getColumn() -> C

	/**
	 * Returns the value stored by this cell
	 *
	 * @return the value
	 */
	func getValue() -> V

	/**
	 * Converts this entry to a {@link Tuple.Of3 Tuple}.
	 *
	 * @return a tuple with this cell's row, column and value
	 */
	func toTuple() -> (R, C, V) = (this.getRow(), this.getColumn(), this.getValue())

	static func cellToString(cell: Cell<_, _, _>) -> String
		= "(\(cell.getRow), \(cell.getColumn)) -> \(cell.getValue)"

	static func cellEquals(cell: Cell<_, _, _>, obj: any!) -> boolean = obj is Cell && cellEquals(cell, obj as Cell)

	static func cellEquals(cell: Cell<_, _, _>, other: Cell<_, _, _>) -> boolean
		= cell.getRow == other.getRow && cell.getColumn == other.getColumn && cell.getValue == other.getValue

	static func cellHashCode(cell: Cell<_, _, _>) -> int
	{
		let row = cell.getRow
		let column = cell.getColumn
		let value = cell.getValue
		let keyHash = row.hashCode * 31 + column.hashCode
		return keyHash * 31 + value.hashCode
	}
}
