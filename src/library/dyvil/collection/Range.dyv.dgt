package dyvil.collection;

import dyvil.collection.immutable.ArrayList;
import dyvil.collection.range._
import dyvil.collection.range.specialized._
import dyvil.util.ImmutableException

import java.io.Serializable
import java.lang.reflect.Array
import java.util.Iterator
import java.util.Spliterator
import java.util.Spliterators
import java.util.function.Consumer
import java.util.function.Function
import java.util.function.Predicate

@LiteralConvertible.FromNil
@LiteralConvertible.FromTuple
@Immutable
public interface Range<+T> extends Queryable<T>, Serializable
{
	@BytecodeName("empty")
	static func apply<T>() -> Range<T> = EmptyRange.instance

#import Base.dgs
#foreach PRIMITIVE_SPECS
	#if HAS_RANGE_CLASS
	@BytecodeName("closed")
	static func apply(PRIMITIVE from, PRIMITIVE to) -> TYPE_NAME##Range = TYPE_NAME##Range(from, to: to)
	
	@BytecodeName("halfOpen")
	static func apply(PRIMITIVE from, PRIMITIVE toExclusive) -> TYPE_NAME##Range = TYPE_NAME##Range(from, toExclusive: toExclusive)

	#end
#end
	
	@BytecodeName("closed")
	static func apply<T extends Rangeable>(T from, T to) -> Range<T>
		= new dyvil.collection.range.closed.ObjectRange<T>(from, to)

	@BytecodeName("halfOpen")
	static func apply<T extends Rangeable>(T from, T toExclusive) -> Range<T>
		= new dyvil.collection.range.halfopen.ObjectRange<T>(from, toExclusive)

	/**
	 * @return the first element in this range
	 */
	func first() -> T

	/**
	 * @return the last element in this range
	 */
	func last() -> T

	/**
	 * Returns the exact number of elements in this range, i.e. the number of elements that would be returned by the
	 * {@link #iterator()}.
	 *
	 * @return the number of elements in this range
	 */
	override func size() -> int

	func asHalfOpen() -> Range<T>

	func asClosed() -> Range<T>

	func isHalfOpen() -> boolean
	
	override func iterator() -> Iterator<T>
	
	override func spliterator() -> Spliterator<T>
	{
		let size = this.size
		var characteristics = Spliterator.ORDERED
		if (size >= 0)
		{
			characteristics |= Spliterator.SIZED
		}
		return Spliterators.spliterator(this.iterator(), size, characteristics)
	}

	override func contains(Object! element) -> boolean

	override func forEach(Consumer<-T> action) -> void

	@Mutating
	override func map(Function<-T, +T> mapper) -> void
		= throw new ImmutableException("map() on Immutable Range")

	@Mutating
	override func flatMap(Function<-T, +Iterable<+T>> mapper) -> void
		= throw new ImmutableException("flatMap() on Immutable Range")

	@Mutating
	override func filter(Predicate<-T> condition) -> void
		= throw new ImmutableException("filter() on Immutable Range")
	
	override func mapped<R>(Function<-T, +R> mapper) -> Queryable<R>
	{
		let builder = new ArrayList.Builder<R>(this.size())
		for (value <- this) builder.add(mapper(value))
		return builder.build();
	}

	override func flatMapped<R>(Function<-T, +Iterable<+R>> mapper) -> Queryable<R>
	{
		let builder = new ArrayList.Builder<R>(this.size)
		for (value <- this) for (result <- mapper(value)) builder.add(result)
		return builder.build
	}
	
	override func filtered(Predicate<-T> condition) -> Queryable<T>
	{
		let builder = new ArrayList.Builder<T>(this.size)
		for (value <- this) if (condition.test(value)) builder.add(value)
		return builder.build()
	}

	// toArray

	func toArray() -> [Object]
	{
		let array = new [Object](this.size)
		this.toArray(0, array);
		return array;
	}

	func toArray<@Reified(Reified.Type.OBJECT_CLASS) T>() -> [T]
	{
		let array = Array.newInstance(class T, this.size) as [T]
		this.toArray(0, array)
		return array
	}

	func toArray([Object!] store) -> void = this.toArray(0, store)

	func toArray(int index, [Object!] store) -> void

	// Copying

	func copy() -> Range<T>

	// toString, equals and hashCode

	override func toString() -> String

	override func equals(Object! obj) -> boolean

	override func hashCode() -> int

	static func rangeEquals(Range<_> range, Object! o) -> boolean
		= o is Range && rangeEquals(range, o as Range)

	static func rangeEquals(Range<_> range, Range<_> other) -> boolean
		= range.first == other.first && range.last == other.last && range.size == other.size

	static func rangeHashCode(Range<_> range) -> int
		= ((range.first.hashCode * 31) + range.last.hashCode) * 31 + range.size
}
