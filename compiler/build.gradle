import org.apache.tools.ant.filters.ReplaceTokens

// --------------- Plugins ---------------

plugins {
	id 'java'
	id 'application'
}

// --------------- Info ---------------

version = '0.44.0'

mainClassName = 'dyvilx.tools.compiler.Main'

// --------------- Source Sets ---------------

sourceSets.main {
	java {
		srcDir '../src/compiler'
	}

	resources {
		srcDir '../src/compiler'
	}
}

// --------------- Dependencies ---------------

dependencies {
	compile project(':library')
	compileOnly project.libraryBootstrapJarFile
}

// --------------- Source Copying ---------------

tasks.register('copyJava', Sync) {
	group 'sources'

	from sourceSets.main.java
	into "$buildDir/src/java/main/"

	include project.javaFilters
	includeEmptyDirs = false

	filesMatching('**/DyvilCompiler.java') {
		filter(ReplaceTokens, beginToken: '$$', endToken: '$$', tokens: [
				version        : rootProject.version,
				libraryVersion : project(':library').version,
				compilerVersion: project.version
		])
	}
}

compileJava {
	dependsOn 'copyJava'
	source = "$buildDir/src/java/main/"
}
